module lop.prattTest;

members {
    testCases
}

import sequences;
import strings;
import lazySequences;

import hat.TestCase;
import duck.assertThat;
import duck.isList;
import duck.equalTo;
import duck.anything;
import duck.MatchResult;

import lop.pratt;
import lop.rules;
import lop.token.Token;
import lop.messages.unexpectedInputError;
import lop.results;

import lop.testing.isSuccess;
import lop.testing.isErrorWithError;

def source fun(value: String) => createStringSource(value, "raw string");

val testCases = listOf(
    TestCase("can parse prefix expression", fun() =>
        assertSuccessfulParse(
            pratt.parser("expression",
                listOf(rules.tokenOfType("identifier")),
                emptyList
            ).rule(),
            listOf(Token("identifier", "blah")),
            equalTo("blah")
        )
    ),
    
    TestCase("can parse simple infix expression", fun() => do {
        def partialCallRule fun(tokens: Sequence[Token]) =>
            rules.sequence()
                .next(rules.token("symbol", "("))
                .capture(expressionParser.rule())
                .next(rules.token("symbol", ")"))
                .map(fun(arg: ExpressionNode) => fun(callee: ExpressionNode) => tuple(callee, arg))
                (tokens);
                
        val expressionParser = pratt.parser("expression",
            listOf(rules.tokenOfType("identifier")),
            listOf(
                pratt.infix("call", partialCallRule)
            )
        );
        
        return assertSuccessfulParse(
            expressionParser.rule(),
            listOf(
                Token("identifier", "print"),
                Token("symbol", "("),
                Token("identifier", "name"),
                Token("symbol", ")")
            ),
            equalTo(tuple("print", "name"))
        );
    }),
    
    TestCase("can parse expression with two left associative operators", fun() => do {
        def partialAddRule fun(tokens: Sequence[Token]) =>
            rules.sequence()
                .next(rules.token("symbol", "+"))
                .capture(expressionParser.leftAssociative("add"))
                .map(fun(arg: ExpressionNode) => fun(callee: ExpressionNode) => tuple("+", callee, arg))
                (tokens);
                
        def partialMultiplyRule fun(tokens: Sequence[Token]) =>
            rules.sequence()
                .next(rules.token("symbol", "*"))
                .capture(expressionParser.leftAssociative("multiply"))
                .map(fun(arg: ExpressionNode) => fun(callee: ExpressionNode) => tuple("*", callee, arg))
                (tokens);
                
        val expressionParser = pratt.parser("expression",
            listOf(rules.tokenOfType("number")),
            listOf(
                pratt.infix("multiply", partialMultiplyRule),
                pratt.infix("add", partialAddRule)
            )
        );
        
        return assertSuccessfulParse(
            expressionParser.rule(),
            listOf(
                Token("number", "1"),
                Token("symbol", "*"),
                Token("number", "2"),
                Token("symbol", "*"),
                Token("number", "3"),
                Token("symbol", "+"),
                Token("number", "4"),
                Token("symbol", "*"),
                Token("number", "5")
            ),
            equalTo(tuple("+", tuple("*", tuple("*", "1", "2"), "3"), tuple("*", "4", "5")))
        );
    }),
    
    TestCase("can parse expression with right associative operators", fun() => do {
        def partialAddRule fun(tokens: Sequence[Token]) =>
            rules.sequence()
                .next(rules.token("symbol", "+"))
                .capture(expressionParser.leftAssociative("add"))
                .map(fun(arg: ExpressionNode) => fun(callee: ExpressionNode) => tuple("+", callee, arg))
                (tokens);
                
        def partialPowerRule fun(tokens: Sequence[Token]) =>
            rules.sequence()
                .next(rules.token("symbol", "^"))
                .capture(expressionParser.rightAssociative("power"))
                .map(fun(arg: ExpressionNode) => fun(callee: ExpressionNode) => tuple("^", callee, arg))
                (tokens);
                
        val expressionParser = pratt.parser("expression",
            listOf(rules.tokenOfType("number")),
            listOf(
                pratt.infix("power", partialPowerRule),
                pratt.infix("add", partialAddRule)
            )
        );
        
        return assertSuccessfulParse(
            expressionParser.rule(),
            listOf(
                Token("number", "1"),
                Token("symbol", "^"),
                Token("number", "2"),
                Token("symbol", "^"),
                Token("number", "3"),
                Token("symbol", "+"),
                Token("number", "4"),
                Token("symbol", "^"),
                Token("number", "5")
            ),
            equalTo(tuple("+", tuple("^", "1", tuple("^", "2", "3")), tuple("^", "4", "5")))
        );
    }),
    
    TestCase("fatal failure in infix rule is returned", fun() =>
        assertFatalParseWithError(
            pratt.parser("expression",
                listOf(rules.tokenOfType("identifier")),
                listOf(pratt.infix("power", fatalRule))
            ).rule(),
            listOf(Token("identifier", "blah")),
            unexpectedInputError("<expected>", "<actual>")
        )
    )
);

def fatalRule fun(tokens: Sequence[Token]) =>
    results.fatal(unexpectedInputError("<expected>", "<actual>"));

def stringSource fun(string: String) =>
    createStringSource(string, "raw string");

def assertFatalParseWithError fun[T] => (rule: Rule[T], input: List[Token], error: Error) => do {
    return assertParse[T](rule, input, isErrorWithError(error));
};

def assertSuccessfulParse fun[T] => (rule: Rule[T], input: List[Token], value: T) => do {
    return assertParse[T](rule, input, isSuccess[T](value));
};

def assertParse fun[T] => (rule: Rule[T], input: List[Token], matcher: Matcher) => do {
    val result = rule(input.toSequence());
    return assertThat[T](result, matcher);
};
